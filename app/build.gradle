plugins {
	id "com.diffplug.spotless" version "6.22.0"
	id 'com.android.application'
	id 'kotlin-android'
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	kotlin {
		target '**/*.kt'
		ktlint()userData(['max_line_length': '100'])
	}
}

android {
	namespace 'com.boringdroid.systemui'
	compileSdk 33

	defaultConfig {
		applicationId "com.boringdroid.systemui"
		minSdkVersion 28
		targetSdkVersion 33
		versionCode 130
		versionName "130"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	sourceSets {
		main.resources.srcDirs += "src/main/SystemUISharedRes"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	kotlinOptions {
		jvmTarget = "1.8"
	}

	signingConfigs {
		debug {
			keyAlias 'androiddebugkey'
			keyPassword 'android'
			storeFile file('plugin.keystore')
			storePassword 'android'
		}
	}


	testOptions {
		unitTests {
			all {
				testLogging {
					events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'

					outputs.upToDateWhen {
						false
					}

					showStandardStreams = false
					exceptionFormat = 'full'
				}
			}

			includeAndroidResources = true
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	lint {
		abortOnError false
	}
}

dependencies {
	// Exclude Kotlin related libraries and to use version in SystemUISharedLib.jar.
	configurations {
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-io'
		implementation.exclude group: 'org.jetbrains', module: 'annotations'
		implementation.exclude group: 'androidx.core'
		implementation.exclude group: 'androidx.annotation'
		implementation.exclude group: 'androidx.collection'
		implementation.exclude group: 'org.jetbrains', module: 'annotations'
	}

	implementation files('libs/SystemUISharedLib.jar')
	implementation('androidx.recyclerview:recyclerview:1.3.0')

	testImplementation 'androidx.test:core:1.5.0'
	testImplementation 'junit:junit:4.13.2'
	testImplementation "org.robolectric:robolectric:4.11.1"
	testImplementation "com.google.truth:truth:1.1.5"
	androidTestImplementation 'androidx.test:runner:1.5.2'
	androidTestImplementation 'androidx.test:rules:1.5.0'
	androidTestImplementation 'com.google.truth:truth:1.1.5'
	androidTestImplementation 'com.google.truth.extensions:truth-java8-extension:1.1.5'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
	androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
