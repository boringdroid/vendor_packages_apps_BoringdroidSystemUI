plugins {
	id "com.diffplug.spotless" version "5.11.0"
	id 'com.android.application'
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		target 'src/**/*.java'
		// apply a specific flavor of google-java-format
		googleJavaFormat('1.8').aosp()
	}
}

android {
	compileSdkVersion 30
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "com.boringdroid.systemui"
		minSdkVersion 28
		targetSdkVersion 30
		versionCode 100
		versionName "100"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	signingConfigs {
		debug {
			keyAlias 'androiddebugkey'
			keyPassword 'android'
			storeFile file('plugin.keystore')
			storePassword 'android'
		}
	}

	lintOptions {
		abortOnError false
	}

	testOptions {
		unitTests {
			all {
				testLogging {
					events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'

					outputs.upToDateWhen {
						false
					}

					showStandardStreams = false
					exceptionFormat = 'full'
				}
			}

			includeAndroidResources = true
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	compileOnly fileTree(include: ['*.jar'], dir: 'libs/compile')
	implementation fileTree(include: ['*.jar'], dir: 'libs/impl')
	// Current system use this version, we should keep it.
	implementation 'androidx.recyclerview:recyclerview:1.2.1'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'androidx.test:core:1.4.0'
	testImplementation 'org.robolectric:robolectric:4.6.1'
	testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
	testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.9'
	testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
	testImplementation 'org.powermock:powermock-classloading-xstream:2.0.9'
	testImplementation 'org.apache.commons:commons-lang3:3.11'
	testImplementation 'com.google.truth:truth:1.1.3'
}
