plugins {
	id "com.diffplug.spotless" version "5.11.0"
	id 'com.android.application'
}
apply plugin: 'kotlin-android'

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	kotlin {
		target '**/*.kt'
		ktlint()userData(['max_line_length': '100'])
	}
}

android {
	compileSdkVersion 30

	defaultConfig {
		applicationId "com.boringdroid.systemui"
		minSdkVersion 28
		targetSdkVersion 30
		versionCode 100
		versionName "100"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	signingConfigs {
		debug {
			keyAlias 'androiddebugkey'
			keyPassword 'android'
			storeFile file('plugin.keystore')
			storePassword 'android'
		}
	}

	lintOptions {
		abortOnError false
	}

	testOptions {
		unitTests {
			all {
				testLogging {
					events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'

					outputs.upToDateWhen {
						false
					}

					showStandardStreams = false
					exceptionFormat = 'full'
				}
			}

			includeAndroidResources = true
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	compileOnly fileTree(include: ['*.jar'], dir: 'libs/compile')
	implementation fileTree(include: ['*.jar'], dir: 'libs/impl')
	// Current system use this version, we should keep it.
	implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.2.0-alpha06'
	implementation "androidx.core:core-ktx:1.3.2"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'androidx.test:core:1.3.0'
	testImplementation "org.robolectric:robolectric:4.5.1"
	testImplementation "org.powermock:powermock-module-junit4:2.0.9"
	testImplementation "org.powermock:powermock-module-junit4-rule:2.0.9"
	testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
	testImplementation "org.powermock:powermock-classloading-xstream:2.0.9"
	testImplementation 'org.apache.commons:commons-lang3:3.12.0'
	testImplementation "com.google.truth:truth:1.1.2"
	androidTestImplementation 'androidx.test:runner:1.3.0'
	androidTestImplementation 'androidx.test:rules:1.3.0'
	androidTestImplementation 'com.google.truth:truth:1.1.3'
	androidTestImplementation 'com.google.truth.extensions:truth-java8-extension:1.1.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
	androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
repositories {
	mavenCentral()
}
